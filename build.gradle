plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'java'
    id "com.bmuschko.docker-remote-api" version "4.6.2"	
}

apply plugin: 'io.spring.dependency-management'
apply from: "$rootDir/gradle/versioning.gradle"

group = 'ch.so.agi.oereb'
//version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
    jcenter()
	mavenCentral()
}

configurations {
    jaxb
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
    implementation 'org.springframework:spring-oxm:5.1.6.RELEASE'   
    implementation 'com.giffing.wicket.spring.boot.starter:wicket-spring-boot-starter:2.1.7'   
    implementation 'de.agilecoders.wicket:wicket-bootstrap-core:3.0.0-M11' // siehe pom.xml des entsprechenden branches   
    implementation 'de.agilecoders.wicket:wicket-bootstrap-extensions:3.0.0-M11' 
    implementation 'org.wicketstuff:wicketstuff-annotation:8.4.0'
    implementation 'org.wicketstuff:wicketstuff-openlayers3:8.4.0'
    implementation 'org.wicketstuff:wicketstuff-openlayers3-bootstrap:8.4.0'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'

    
    compile group: 'javax.xml.bind',name:'jaxb-api', version:'2.3.0'
    compile group: 'com.sun.xml.bind',name:'jaxb-core', version:'2.3.0'
    compile group: 'com.sun.xml.bind',name:'jaxb-impl', version:'2.3.0'
    compile group: 'javax.activation', name: 'activation', version: '1.1.1'
	
    jaxb (
        'com.sun.xml.bind:jaxb-xjc:2.2.7',
        'com.sun.xml.bind:jaxb-impl:2.2.7'
    )	
}

def generatedXjcSources = "$buildDir/xjc/java"

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java',"$generatedXjcSources"]
        }
        resources {
            srcDirs = ['src/main/java','src/main/resources']
        }
    }
}

task jaxb {
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    inputs.files fileTree(dir: 'src/main/xsd', include: ['*.xsd'])
    inputs.files fileTree(dir: 'src/main/xjb', include: ['*.xjb'])
    outputs.dir "$generatedXjcSources"
    
    doLast {
        mkdir "$generatedXjcSources"

        ant.taskdef(
            name: 'xjc',
            classname: 'com.sun.tools.xjc.XJCTask',
            classpath: configurations.jaxb.asPath
        )

        ant.xjc(
            destdir: "$generatedXjcSources",
            language: 'XMLSCHEMA',
            extension: true
        ) {
            schema(dir: "src/main/xsd", includes: "OeREB/**/*.xsd,gml/3.2/gml.xsd")
            binding(dir: "src/main/xjb", includes: "global.xjb,OeREB.xjb,xmldsig-core-schema.xjb,gml.xjb,iso-19139.xjb,xlink.xjb")
            //arg(value: '-verbose')
        }
    }
}
compileJava.dependsOn jaxb

// Slighly better ("thinner") docker image handling
task deleteFilesJarDir(type: Delete) {
    delete fileTree("${buildDir}/dependency/")//.listFiles()
    delete file("${buildDir}/dependency/")
}

task unzipJar(type: Copy) {
    from zipTree(file("${buildDir}/libs/oereb-client-"+version+".jar"))
    into file("${buildDir}/dependency/")
}
unzipJar.dependsOn(deleteFilesJarDir)
bootJar.finalizedBy(unzipJar)

// Build Docker image
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

docker {
    registryCredentials {
        username = System.env.dockerRegistryUser
        password = System.env.dockerRegistryPass
    }
}

task buildDockerImage(type: DockerBuildImage) {
    description = "Build docker image."
    inputDir = project.projectDir
    tags = ["sogis/oereb-client:"+ version, "sogis/oereb-client:latest"]
    labels = ["oereb-client.created": new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date()), 
            "oereb-client.git_commit":  getCheckedOutGitCommitHash(), 
            "oereb-client.travis_build": version]
}

task pushDockerImage_One(type: DockerPushImage) {
    description = "Push docker image to hub.docker.com (images are UNTESTED)."
    imageName = buildDockerImage.tags.get().toArray()[0]
}

task pushDockerImage_Two(type: DockerPushImage) {
    description = "Push docker image to hub.docker.com (images are UNTESTED)."
    imageName = buildDockerImage.tags.get().toArray()[1]
}

task pushDockerImages() {
    description = "Collect all push images tasks."
}

pushDockerImages.dependsOn {
    tasks.findAll { task -> task.name.startsWith('pushDockerImage_') }
}

import java.text.SimpleDateFormat

def getCheckedOutGitCommitHash() {
    'git log -1 --pretty=%H'.execute().text.trim()
}

def getTimestamp() {
    def date = new Date()
    return date.format('yyyy-MM-dd HH:mm:ss')
}

// Create a static text file with same meta information as the docker labels
task versionTxt()  {
    outputs.upToDateWhen { false }
    doLast {
        new File("version.txt").text = """
Version: $version
Revision: ${getCheckedOutGitCommitHash()}
Buildtime: ${getTimestamp()}
Application-name: avdpool
"""
    }
}
task copyVersionTxt(type: Copy) {
    from "version.txt"
    into "src/main/resources/static/"
}
copyVersionTxt.dependsOn(versionTxt)
build.dependsOn(copyVersionTxt)
classes.dependsOn(copyVersionTxt)

